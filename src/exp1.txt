/*
   3 sprites
   base - covers entire screen
   line - for drawing line
   dot - for drawing dot
   Once - fill base
   Each iteration
   draw line - export to base
   draw dot - export to base
   export base to screen
*/

#include <Arduino.h>
#include <SPI.h>
#include <TFT_eSPI.h> // Hardware-specific library


#define TFT_GREY 0x5AEB

#define BASE_COLOR TFT_PURPLE

#define LINE_HEIGHT 10
#define LINE_COLOR TFT_YELLOW

#define DOT_RADIUS 20
#define DOT_COLOR TFT_CYAN

#define BASE_BORDER 1   // gap around base sprite and screen

TFT_eSPI tft = TFT_eSPI()  ;    // Invoke custom library
TFT_eSprite baseSprite = TFT_eSprite(&tft);
TFT_eSprite lineSprite = TFT_eSprite(&tft);
TFT_eSprite dotSprite = TFT_eSprite(&tft);

#define BLPIN 22
unsigned int sWidth,sHeight,dotYpos,dotXpos,lineXpos,lineYpos;
bool dotLR;

void setup(void) {
  Serial.begin(115200);
  sWidth = tft.width();
  sHeight = tft.height();
  pinMode(BLPIN,OUTPUT);
  digitalWrite(BLPIN,HIGH);
  tft.init();
  tft.setRotation(0);
  tft.fillScreen(BASE_COLOR); 
  // create base sprite & fill it
  baseSprite.createSprite(sWidth-(BASE_BORDER*2),sHeight-(BASE_BORDER*2));  // cannot have sprite exact same size as screen
  baseSprite.fillSprite(BASE_COLOR);
  // create line sprite & locate in center of base sprite
  lineSprite.createSprite(sWidth-(BASE_BORDER*2),LINE_HEIGHT);
  lineSprite.fillRect(0,0,lineSprite.width(),lineSprite.height(),LINE_COLOR);
  lineXpos = 0;
  lineYpos =  (baseSprite.height()-LINE_HEIGHT)/2;   // relative to base
  // draw dot across center of base sprite
  dotSprite.createSprite(DOT_RADIUS*2,DOT_RADIUS*2);
  dotSprite.fillSprite(TFT_BLACK);
  dotSprite.fillCircle(DOT_RADIUS,DOT_RADIUS,DOT_RADIUS,DOT_COLOR);
  dotXpos = 0;
  dotYpos = baseSprite.height()/2 - DOT_RADIUS;
}

void loop() {
  // repair base sprite
  baseSprite.fillSprite(BASE_COLOR);
  // draw line to base
  lineSprite.pushToSprite(&baseSprite,lineXpos,lineYpos);
  // draw dot to base
  Serial.printf("x %d y %d\r\n",dotXpos,dotYpos);
  dotSprite.pushToSprite(&baseSprite,dotXpos++,dotYpos,TFT_BLACK);  // ignore background color
  if (dotXpos == baseSprite.width())
    dotXpos = 0;
  // push base to screen
  baseSprite.pushSprite(BASE_BORDER,BASE_BORDER);  // do not ignore background color
}


